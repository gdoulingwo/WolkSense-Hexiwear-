apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "com.wolkabout.hexiwear"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 29
        versionName "1.29"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/org.androidannotations.plugin.AndroidAnnotationsPlugin'
        exclude 'asm-license.txt'
    }
    if (project.hasProperty("hexiwear.properties") && file(project.property("hexiwear.properties")).exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(file(project.property("hexiwear.properties"))))

        signingConfigs {
            release {
                storeFile file(properties['keystore'])
                storePassword properties['keystore.password']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

def SupportVersion = '25.3.1'
def AAVersion = '4.0.0'
def ParcelerVersion = '1.1.6'

dependencies {
    // Android support libraries
    compile "com.android.support:appcompat-v7:$SupportVersion"
    compile "com.android.support:design:$SupportVersion"
    compile "com.android.support:cardview-v7:$SupportVersion"

    // Android annotations
    compile "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:rest-spring-api:$AAVersion"
    compile "org.androidannotations:rest-spring:$AAVersion"

    // Parceler
    compile "org.parceler:parceler-api:$ParcelerVersion"
    apt "org.parceler:parceler:$ParcelerVersion"

    // view
    compile 'com.txusballesteros:snake:1.0'
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
    }
}
